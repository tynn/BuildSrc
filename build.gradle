allprojects {
    apply plugin: 'jacoco'
    repositories { mavenCentral() }
}

subprojects {
    apply plugin: 'groovy'
    apply plugin: 'maven'

    dependencies {
        compile gradleApi()
        compile localGroovy()
        testCompile gradleTestKit()
        testCompile 'junit:junit:4.12'
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts { archives sourcesJar }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn { subprojects*.test }
    executionData fileTree(project.rootDir.absolutePath)
                    .include("**/build/jacoco/*.exec")

    subprojects.each { sourceSets it.sourceSets.main }

    reports {
        html.enabled false
        csv.enabled false
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
